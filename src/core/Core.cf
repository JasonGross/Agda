
-- Grammar for AgdaCore.

layout toplevel;

-- Program
Program. Prog ::= [Def] ;

-- Definitions
Sig.	    Def ::= Ident ":" Type ;
PatternDef. Def ::= Ident [Ident] Pattern1 "=" Term ;
ExplDef.    Def ::= Ident "=" Term ;

separator Def ";" ;
separator Ident "" ;

-- Types
Pi.  Type  ::= "(" Ident ":" Type ")" "->" Type ;
fun. Type  ::= Type1 "->" Type ;
El.  Type1 ::= "El" Term2 ;
Set. Type1 ::= "Set" ;

define fun a b = Pi (Ident "_") a b ;

coercions Type 1;

-- Terms
Lam. Term  ::= "\\" Ident "." Term ;
App. Term1 ::= Term1 Term2 ;
Var. Term2 ::= Ident ;
coercions Term 2;

-- Patterns
AppP. Pattern  ::= Ident [Pattern1] ;
varP. Pattern1 ::= Ident ;
coercions Pattern 1;

separator nonempty Pattern1 "";

define varP x = AppP x [] ;

comment "--";
comment "{-" "-}";

